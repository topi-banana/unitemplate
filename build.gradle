plugins {
    id 'maven-publish'
    id 'com.github.hierynomus.license' version '0.16.1' apply false
    id 'dev.architectury.loom' version '1.7-SNAPSHOT' apply false

    // https://github.com/ReplayMod/preprocessor
    // https://github.com/Fallen-Breath/preprocessor
    id 'com.replaymod.preprocess' version 'ce1aeb2b'

    // https://github.com/Fallen-Breath/yamlang
    id 'me.fallenbreath.yamlang' version '1.4.0' apply false
}

preprocess {
    def fabric_mc12004 = createNode('fabric-1.20.4', 1_20_04, '')
    def fabric_mc12006 = createNode('fabric-1.20.6', 1_20_06, '')
    def fabric_mc12103 = createNode('fabric-1.21.3', 1_21_03, '')

    def forge_mc12004 = createNode('forge-1.20.4', 1_20_04, '')
    def forge_mc12006 = createNode('forge-1.20.6', 1_20_06, '')
    def forge_mc12103 = createNode('forge-1.21.3', 1_21_03, '')

    def neoforge_mc12006 = createNode('neoforge-1.20.6', 1_20_06, '')
    def neoforge_mc12103 = createNode('neoforge-1.21.3', 1_21_03, '')

    // fabric
    fabric_mc12004.link(fabric_mc12006, null)
    fabric_mc12006.link(fabric_mc12103, null)

    // forge
    fabric_mc12004.link(forge_mc12004, null)

    forge_mc12004.link(forge_mc12006, null)
    forge_mc12006.link(forge_mc12103, null)

    // neoforge
    fabric_mc12006.link(neoforge_mc12006, null)

    neoforge_mc12006.link(neoforge_mc12103, null)
}

tasks.register('buildAndGather') {
    subprojects {
        dependsOn project.tasks.named('build').get()
    }
    doFirst {
        println 'Gathering builds'
        def buildLibs = {
            p -> p.buildDir.toPath().resolve('libs')
        }
        delete fileTree(buildLibs(rootProject)) {
            include '*'
        }
        subprojects {
            copy {
                from(buildLibs(project)) {
                    include '*.jar'
                    exclude '*-dev.jar', '*-sources.jar'
                }
                into buildLibs(rootProject)
                duplicatesStrategy DuplicatesStrategy.INCLUDE
            }
        }
    }
}