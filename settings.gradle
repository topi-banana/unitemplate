pluginManagement {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        maven { url "https://maven.fabricmc.net/" }
        maven { url "https://maven.architectury.dev" }
        maven { url "https://maven.minecraftforge.net" }
        maven { url "https://maven.neoforged.net/releases/" }
    }
}

plugins {
    id "dev.kikugie.stonecutter" version "0.4.4"
    id "org.gradle.toolchains.foojay-resolver-convention"  version "0.8.0"
}

stonecutter {
    centralScript = "build.gradle"

    shared {
        def mc = { String version, String... loaders ->
            for (loader in loaders) {
                vers("${loader}-${version}", version)
            }
        }

        mc("1.14.4", "fabric", "forge")
        mc("1.15.2", "fabric", "forge")
        mc("1.16.5", "fabric", "forge")
        mc("1.17.1", "fabric", "forge")
        mc("1.18.2", "fabric", "forge")
        mc("1.19.4", "fabric", "forge", "paper")
        mc("1.20.4", "fabric", "forge", "paper")
        mc("1.20.6", "fabric", "forge", "neoforge", "paper")
        mc("1.21.3", "fabric", "forge", "neoforge", "paper")
    }
    create(rootProject)
}