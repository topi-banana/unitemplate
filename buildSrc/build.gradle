import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java'
    id 'java-gradle-plugin'
    id 'com.gradleup.shadow' version '9.0.0-beta8'
}

group 'com.vulpeus.mod_relocator'

configurations {
    shadowMe
    compileOnly.extendsFrom shadowMe
}

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    // Magic
    shadowMe('me.lucko:jar-relocator:1.7') {
        exclude group: 'org.ow2.asm'
    }
    shadowMe 'org.ow2.asm:asm:9.7'
    shadowMe 'org.ow2.asm:asm-commons:9.7'

    shadowMe 'net.lingala.zip4j:zip4j:2.11.5'
    shadowMe 'commons-io:commons-io:2.14.0'

    shadowMe 'com.google.code.gson:gson:2.12.1'
}

tasks.named('shadowJar', ShadowJar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    configurations = [project.configurations.getByName("shadowMe")]
    archiveClassifier.set(null)

    relocate 'me.lucko.jarrelocator', 'com.vulpeus.libs.me.lucko.jarrelocator'
    relocate 'org.objectweb.asm', 'com.vulpeus.libs.org.objectweb.asm'
    relocate 'net.lingala.zip4j', 'com.vulpeus.libs.net.lingala.zip4j'
    relocate 'com.google.gson', 'com.vulpeus.libs.com.google.gson'
}

gradlePlugin {
    plugins {
        myPlugin {
            id = 'com.vulpeus.mod_relocator'
            implementationClass = 'com.vulpeus.mod_relocator.RelocatorPlugin'
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile) {
    sourceCompatibility = 8
    options.release = 8
}

jar.finalizedBy(shadowJar)
